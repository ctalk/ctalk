/* $Id: X11ListPane,v 1.4 2020/06/26 23:03:39 rkiesling Exp $ -*-C-*-*/

/*
  This file is part of Ctalk.
  Copyright © 2020  Robert Kiesling, rk3314042@gmail.com.
  Permission is granted to copy this software provided that this copyright
  notice is included in all source code modules.

  This library is free software; you can redistribute it and/or
  modify it under the terms of the GNU Lesser General Public
  License as published by the Free Software Foundation; either
  version 2 of the License, or (at your option) any later version.

  This library is distributed in the hope that it will be useful,
  but WITHOUT ANY WARRANTY; without even the implied warranty of
  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
  Lesser General Public License for more details.

  You should have received a copy of the GNU Lesser General Public
  License along with this library; if not, write to the Free Software
  Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307  USA
*/

/* 
   X11ListPane Class

   Resources

   borderColor              "gray"
   scrollWidth              10
   focusBorderWidth         2
   borderWidth              1

*/

X11CanvasPane class X11ListPane;

X11ListPane instanceVariable hover Boolean false;

X11ListPane instanceMethod new (String paneName) {
  Method new exposeMethod, enterMethod, leaveMethod;

  X11ListPane super new paneName;
  
  exposeMethod definedInstanceMethod "X11ListPane", "onExpose";
  paneName setMethodHandler "expose", exposeMethod;

  enterMethod definedInstanceMethod "X11ListPane", "onEnter";
  paneName setMethodHandler "enternotify", enterMethod;

  leaveMethod definedInstanceMethod "X11ListPane", "onLeave";
  paneName setMethodHandler "leavenotify", leaveMethod;

  paneName resources atPut "borderColor", "gray";
  paneName resources atPut "scrollWidth", 10;
  paneName resources atPut "borderWidth", 1;
  paneName resources atPut "focusBorderWidth", 2;

  return paneName;
}

X11ListPane instanceMethod onEnter (Object subPane,
				    InputEvent __event) {
  subPane hover = true;
  subPane draw;
  subPane refresh;
}

X11ListPane instanceMethod onLeave (Object subPane,
				    InputEvent __event) {
  subPane hover = false;
  subPane draw;
  subPane refresh;
}


X11ListPane instanceMethod onExpose (InputEvent e, Object subPane) {
  self draw;
  self refresh;
}

X11ListPane instanceMethod draw (void) {

  Integer new border;
  self background "white";
  self clear;

  self pen colorName = self resources at "borderColor";
  if (self hover) {
    self pen width = self resources integerAt "focusBorderWidth";
  } else {
    self pen width = self resources integerAt "borderWidth";
  }
  self drawRectangle 0, 0, self size x, self size y;

  self pen colorName = "darkgray";
  self pen width = 2;
#if 0
  if (self hover) {
    border = self resources integerAt "focusBorderWidth";
  } else {
    border = self resources integerAt "borderWidth";
  }
  self drawLine self resources integerAt "scrollWidth",
    border,
    self resources integerAt "scrollWidth", 
    self size y - border;
#else  
  if (self hover) {
    self drawLine self resources integerAt "scrollWidth",
      self resources integerAt "focusBorderWidth",
      self resources integerAt "scrollWidth", 
      self size y - self resources integerAt "focusBorderWidth";
  } else {
    self drawLine self resources integerAt "scrollWidth",
      self resources integerAt "borderWidth",
      self resources integerAt "scrollWidth", 
      self size y - self resources integerAt "borderWidth";

  }
#endif  
}

